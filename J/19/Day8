# -*- mode: org -*-
#+title: [[https://adventofcode.com/2019/day/8][Day 8: Space Image Format]]

** Setup

  Split into 100 (150=6x25) "layers."

#+begin_src j :session :exports code
load '~/code/aoc/aoc.ijs'
input =: aoc 2019;8
layers =: _1 }. _150 [\ input
#+end_src

#+RESULTS:

  Select index where the ~minimimum~ is found. ~color~ finds indices
  that aren't transparent and selects first such.

#+begin_src j :session :exports code
count =: [: +/"1 =
minimize =: [ {~ [: (i. <./) ] 
color =: [: {. [ {~ [: I. '2'&~: 
#+end_src

#+RESULTS:

** Part A

   Select the row with the fewest '0's, multiply by the # of '1's and
   '2's.

#+begin_src j :session :exports code
partA =: (count&'1' * count&'2') @ (minimize count&'0')
#+end_src

#+RESULTS:

** Part B

   Transpose the layers to stack the pixels, find the colors, rebuild
   in the right shape (6x25), and finally make it readable.

#+begin_src j :session :exports code
partB =: ' J' {~ '1' = 6 25 $ color"1 @ |:
#+end_src

#+RESULTS:

** Reults

#+begin_src j :session :exports both
(partA;partB) layers
#+end_src

#+RESULTS:
: ┌────┬─────────────────────────┐
: │2064│J  J  JJ  J  J JJJJ  JJ  │
: │    │J J  J  J J  J    J J  J │
: │    │JJ   J  J J  J   J  J  J │
: │    │J J  JJJJ J  J  J   JJJJ │
: │    │J J  J  J J  J J    J  J │
: │    │J  J J  J  JJ  JJJJ J  J │
: └────┴─────────────────────────┘


  

