(load "~/code/aoc/load.ss")

(define program
  (parameterize ((advent-year 19) (advent-day 13))
    (with-input-from-file (advent-file) parse-intcode)))

(define empty 0)
(define (empty? x) (= empty x))
(define wall 1)
(define (wall? x) (= wall x))
(define block 2)
(define (block? x) (= block x))
(define horizontal-paddle 3)
(define (horizontal-paddle? x) (= horizontal-paddle x))
(define ball 4)
(define (ball? x) (= ball x))

(define *ball-x*)
(define *paddle-x*)
(define *score*)
(define *screen13* (make-eqv-hashtable))

(define (paint machine)
  (run-until-halt machine)
  (let loop ((instructions (read-output machine)))
    (match instructions
      ((-1 0 score instructions ...)
       (set! *score* score)
       (loop instructions))
      ((x y pixel instructions ...)
       (when (ball? pixel) (set! *ball-x* x))
       (when (horizontal-paddle? pixel) (set! *paddle-x* x))
       (hashtable-set! *screen13* (make-rectangular x y) pixel)
       (loop instructions))
      (else (void)))))

(define (locate object)
  (filter-map (lambda (z.o)
                (and (= (cdr z.o) object)
                     (car z.o)))
              (vector->list (hashtable-cells *screen13*))))

(define (partA)
  (define m (intcode program))
  (paint m)
  (length (locate block)))

(define (partB)
  (define m (intcode program))
  (intcode-set! m 0 2)
  (let run ()
    (paint m)
    (unless (done? m)
      (send-input m (sign (- *ball-x* *paddle-x*)))
      (run)))
  *score*)
